buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:0.42.0"
    }
}

plugins {
    id 'java'
    id "org.jetbrains.kotlin.jvm" version "1.7.20"
    id 'maven-publish'
    id 'org.jetbrains.dokka' version '1.7.10'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id "com.github.ben-manes.versions" version "0.42.0"
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.7.20'
}

apply plugin: "com.github.ben-manes.versions"

group 'com.github.kwebio'
version '0.12.5'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://plugins.gradle.org/m2' }
}

test {
    useJUnitPlatform()
    systemProperty('sel.jup.default.browser', System.getProperty('sel.jup.default.browser'))
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.jsoup:jsoup:1.15.3'
    implementation 'com.google.guava:guava:31.1-jre'

    //////////////////////////////
    // Kotlin library dependencies
    //////////////////////////////

    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.7.20"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.20"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.0'

    ////////////////////
    // Ktor dependencies
    ////////////////////
    implementation "io.ktor:ktor-server-jetty:2.1.2"
    implementation "io.ktor:ktor-server-websockets:2.1.2"
    implementation "io.ktor:ktor-server-default-headers:2.1.2"
    implementation "io.ktor:ktor-server-compression:2.1.2"
    implementation "io.ktor:ktor-server-caching-headers:2.1.2"
    implementation "io.ktor:ktor-network-tls-certificates:2.1.2"

    implementation 'io.mola.galimatias:galimatias:0.2.1'

    implementation 'io.github.microutils:kotlin-logging:3.0.0'
    //compile "org.reflections:reflections:0.10.2"

    implementation 'com.github.yamamotoj:cached-property-kotlin:0.1.0'

    ///////////////////////////
    // Dependencies for testing
    ///////////////////////////
    // Pinned to 5.4.2 for now since there are issues with test discovery in 5.5.0
    // See: https://github.com/kotest/kotest/issues/3223
    testApi(platform('io.kotest:kotest-bom:5.4.2'))
    testApi(platform('org.junit:junit-bom:5.9.1'))

    testImplementation 'io.kotest:kotest-runner-junit5'
    testImplementation 'io.kotest:kotest-assertions-core'

    testImplementation 'ch.qos.logback:logback-classic:1.4.1'
    testImplementation 'io.github.bonigarcia:selenium-jupiter:3.3.5'

    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
//    compile 'org.jsoup:jsoup:1.15.3'
}

// Not sure why this is necessary, seems like a dependency is turning on build scans.
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

//This is required because kotlin compiler targets 1.6 by default (https://stackoverflow.com/questions/41113268)
//https://kotlinlang.org/docs/reference/using-gradle.html#attributes-specific-for-jvm
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

shadowJar {
    archiveBaseName = project.name
    archiveClassifier = 'all'
    manifest {
        attributes 'Main-Class': 'kweb.demos.todo.TodoAppKt'
    }
    mergeServiceFiles()
}

dokkaHtml {
    dokkaSourceSets {
        configureEach {
            samples.from(files("src/main/kotlin/samples.kt"))
        }
    }
}
