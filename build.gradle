buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:0.27.0"
    }
}

plugins {
    id 'java'
    id "org.jetbrains.kotlin.jvm" version '1.3.60'
    id 'maven'
    id 'org.jetbrains.dokka' version '0.10.0'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "com.github.ben-manes.versions" version "0.27.0"

}

apply plugin: "com.github.ben-manes.versions"

ext {
    dokka_version = '0.10.0'
    ktor_version = '1.3.0-rc2'
}

group 'io.kweb'
version '0.5.9'

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/kotlin/ktor" }
    maven { url 'https://jitpack.io' }
    maven { url 'https://plugins.gradle.org/m2' }
    jcenter()
}

test {
    useJUnitPlatform()
    systemProperty('sel.jup.default.browser', System.getProperty('sel.jup.default.browser'))
}

dependencies {
    compile 'com.github.salomonbrys.kotson:kotson:2.5.0'
    compile 'org.apache.commons:commons-lang3:3.9'
    compile 'commons-io:commons-io:2.6'
    compile 'org.jsoup:jsoup:1.12.1'

    //////////////////////////////
    // Kotlin library dependencies
    //////////////////////////////
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    //compile "org.jetbrains.kotlin:kotlin-reflect"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2-1.3.60-eap-76'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.3.2-1.3.60-eap-76'

    ////////////////////
    // Ktor dependencies
    ////////////////////
    compile "io.ktor:ktor-server-core:$ktor_version"
    compile "io.ktor:ktor-server-jetty:$ktor_version"
    compile "io.ktor:ktor-websockets:$ktor_version"
    compile "io.ktor:ktor-network-tls-certificates:$ktor_version"

    compile 'io.mola.galimatias:galimatias:0.2.1'

    compile 'io.github.microutils:kotlin-logging:1.7.8'
    compile "org.reflections:reflections:0.9.11"

    compile 'com.github.kwebio:shoebox:0.2.33'

    compile 'com.github.yamamotoj:cached-property-kotlin:0.1.0'


    ///////////////////////////
    // Dependencies for testing
    ///////////////////////////
    testCompile 'org.amshove.kluent:kluent:1.57'
    testCompile 'io.kotlintest:kotlintest-runner-junit5:3.4.2'
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'
    testCompile "io.github.bonigarcia:selenium-jupiter:3.3.2"
    testCompile "org.junit.jupiter:junit-jupiter-api:5.6.0-M1"
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0-M1'
}

// Not sure why this is necessary, seems like a dependency is turning on build scans.
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'html'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
}

//This is required because kotlin compiler targets 1.6 by default (https://stackoverflow.com/questions/41113268)
//https://kotlinlang.org/docs/reference/using-gradle.html#attributes-specific-for-jvm
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

shadowJar {
    archiveBaseName = project.name
    archiveClassifier = 'all'
    manifest {
        attributes 'Main-Class': 'io.kweb.demos.todo.TodoAppKt'
    }
    mergeServiceFiles()
}